use core {*}

input_as_pairs :: () -> ([..] i32, [..] i32) {
    list1, list2: [..] i32

    for p in os.get_contents("inputs/day01.txt") |> str.split_iter("\n") {
        a, b := str.bisect(p, "   ")
        v1 := conv.parse(i32, a)!
        v2 := conv.parse(i32, b)!

        list1->push(v1)
        list2->push(v2)
    }

    return list1, list2
}

part1 :: () {
    list1, list2 := input_as_pairs()

    Slice.sort(list1, (a, b) => a - b)
    Slice.sort(list2, (a, b) => a - b)

    Iterator.zip(
        Iterator.from(list1)
        Iterator.from(list2)
    )
    |> Iterator.map(x => math.abs(x.first - x.second))
    |> Iterator.sum()
    |> printf("Part 1: {}\n", _)
}

part2 :: () {
    list1, list2 := input_as_pairs()

    total: u64 = 0
    for v in list1 {
        total += ~~(v * Slice.count_where(list2, [x](x == v)) )
    }

    printf("Part 2: {}\n", total)
}

main :: () {
    part1()
    part2()
}
